<?xml version="1.0" encoding="utf-8" ?>

<component name="MainScene" extends="Scene" initialFocus="downloadLabelList">

  <script type="text/brightscript">

    <![CDATA[

    sub init()
      m.state = "downloads"

      m.top.backgroundURI = "pkg:/images/rsgde_bg_hd.jpg"

      m.labelList = m.top.findNode("downloadLabelList")
      m.videoPlayer = m.top.findNode("videoPlayer")

      m.top.removeChild(m.videoPlayer)

      m.readListTask = createObject("roSGNode", "DownloadListReader")
      m.readListTask.observeField("downloadList", "showDownloadList")
      m.readListTask.downloadsUri = "https://BASE_DOMAIN/api/downloads"
      m.readListTask.control = "RUN"
    end sub

    function onKeyEvent(key as String, press as Boolean) as Boolean
      if press then
        if key = "back" then
          if m.state = "files" then
            showDownloadList()
            return true
          else if m.state = "playing" then
            stopPlaying()
            return true
          end if
        else if key = "OK" then
          itemSelected()
          return true
        end if
      end if
      return false
    end function

    sub itemSelected()
      idx = m.labelList.itemSelected
      if m.state = "downloads"
        hash = m.readListTask.downloadHashes[idx]
        m.readFilesTask = createObject("roSGNode", "FileListReader")
        m.readFilesTask.observeField("fileList", "showFileList")
        m.readFilesTask.hash = hash
        m.readFilesTask.control = "RUN"
      else if m.state = "files"
        title = m.readFilesTask.fileList.getChild(idx).title
        videoUrl = m.readFilesTask.fileUrls[idx]
        playVideo(title, videoUrl)
      end if
    end sub

    sub showDownloadList()
      m.state = "downloads"
      showContent(m.readListTask.downloadList)
    end sub

    sub showFileList(updated)
      m.state = "files"
      showContent(m.readFilesTask.fileList)
    end sub

    sub showContent(content)
      m.labelList.content = content

      rect = m.labelList.boundingRect()
      centerX = (1280 - rect.width) / 2
      centerY = (720 - rect.height) / 2
      m.labelList.translation = [centerX, centerX]
      m.labelList.setFocus(true)
    end sub

    sub playVideo(title, url)
      m.state = "playing"

      videoContent = createObject("RoSGNode", "ContentNode")
      videoContent.url = url
      videoContent.title = title
      REM videoContent.streamformat = "mkv"

      m.top.appendChild(m.videoPlayer)
      m.top.removeChild(m.labelList)
      m.videoPlayer.content = videoContent
      m.videoPlayer.control = "play"
      m.videoPlayer.setFocus(true)
    end sub

    sub stopPlaying()
      m.state = "files"

      m.videoPlayer.control = "stop"
      m.top.removeChild(m.videoPlayer)
      m.top.appendChild(m.labelList)
      m.labelList.setFocus(true)
    end sub

    ]]>

  </script>

  <children>

    <LabelList id="downloadLabelList" itemSize="[600,92]" />
    <Video id="videoPlayer" />

  </children>

</component>
